apply plugin: 'com.android.library'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "4.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}
dependencies {
    // 导入jar包
    provided fileTree(include: ['*.jar'], dir: 'libs')
    // 必要支持
    provided 'com.android.support:appcompat-v7:25.3.1'
    provided 'com.android.support:support-v4:25.3.1'
    provided 'com.android.support:design:25.3.1'
    // Face++
    provided(name: 'MGLicenseManagerSDK-0.3.0', ext: 'aar')
    provided(name: 'MGFaceppSDK-0.5.2', ext: 'aar')
    // MYNTEYE
    provided(name: 'mynteye_d_android_sdk-release_1.2.3_20190411111612', ext: 'aar')
    // RxJava
    provided 'io.reactivex.rxjava2:rxjava:2.1.0'
    provided 'io.reactivex.rxjava2:rxandroid:2.0.1'
    // Glide
    provided 'com.github.bumptech.glide:glide:3.7.0'
    // Retrofit
    provided 'com.squareup.retrofit2:retrofit:2.3.0'
    // Retrofit通过GSON将结果转换为Bean对象
    provided 'com.squareup.retrofit2:converter-gson:2.3.0'
    // 让Retrofit支持RxJava
    provided 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
}

task releaseJar(type: Copy) {
    //SDK-jar名称
    String JAR_NAME = 'IBenRobotSDK_' + android.defaultConfig.versionName + '.jar'
    //设置拷贝的文件目录
    from('build/intermediates/bundles/debug/classes.jar')
    //jar包存放的文件目录
    into('build/outputs/')
    //include ,exclude参数来设置过滤，也可以直接写在from()的文件路径里
    include('classes.jar')
    //重命名
    rename('classes.jar', JAR_NAME)
}
android.libraryVariants.all {
    it.generateBuildConfig.enabled = false
}